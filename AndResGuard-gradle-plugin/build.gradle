apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = '1.0.0'
group = "com.tencent.mm"

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile project(':AndResGuard-core')
}

repositories {
    mavenCentral()
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../local_repo'))
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    publications = ['AndResPorguard']
    dryRun = false //Whether to run this as dry-run, without deploying
    pkg {
        repo = "maven"
        name = "AndResGuard-gradle-plugin"
        desc = 'Android Resource Proguard Plugin'
        websiteUrl = 'https://github.com/shwenzhang/AndResGuard'
        issueTrackerUrl = 'https://github.com/shwenzhang/AndResGuard/issues'
        vcsUrl = 'https://github.com/shwenzhang/AndResGuard.git'
        licenses = ["Apache-2.0"]
        publish = true
    }
}

publishing {
    publications {
        AndResPorguard(MavenPublication) {
            from components.java
            groupId 'com.tencent.mm'
            artifactId 'AndResGuard-gradle-plugin'
        }
    }
}